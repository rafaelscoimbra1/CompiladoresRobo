Programa -> programainicio ListaDeclaracao execucaoinicio ListaComando fimexecucao fimprograma
Declaracao -> definainstrucao ID como Comando
ListaDeclaracao -> Declaracao ListaDeclaracao
ListaDeclaracao -> ''
Comando -> Bloco
Comando -> Iteracao
Comando -> Laco
Comando -> Condicional
Comando -> Instrucao
ListaComando -> Comando ListaComando
ListaComando -> ''
Bloco -> inicio ListaComando fim
Iteracao -> repita NUM vezes Comando fimrepita
Laco -> enquanto Condicao faca Comando fimpara
Condicional -> se Condicao entao Comando fimse ComplementoCondicional
ComplementoCondicional -> senao Comando fimsenao
ComplementoCondicional -> ''
Sentido -> esquerda
Sentido -> direita
Instrucao -> mova NUM passos
Instrucao -> vire para Sentido
Instrucao -> ID
Instrucao -> pare
Instrucao -> finalize
Instrucao -> apague lampada
Instrucao -> acenda lampada
Instrucao -> aguarde ate Condicao
Direcao -> esquerda
Direcao -> direita
Direcao -> frente
Estado -> pronto
Estado -> parado
Estado -> ocupado
Estado -> movimentando
Condicao -> robo Estado
Condicao -> Direcao robo bloqueada
Condicao -> lampada acesa a Direcao